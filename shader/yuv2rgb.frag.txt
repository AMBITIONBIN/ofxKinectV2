uniform sampler2D yPlane;
uniform sampler2D rPlane;
uniform sampler2D bPlane;

void main()
{
	const ivec4 lut = ivec4(2, 1, 0, 3);

     // Compute real plane sizes (4 samples per pixel)
	ivec2 ySize = textureSize(yPlane, 0);
	ivec2 rSize = textureSize(rPlane, 0);
	ivec2 bSize = textureSize(bPlane, 0);
	
	ySize.x <<= 2;
	rSize.x <<= 2;
	bSize.x <<= 2;

	// Positions
	vec2 coord = gl_TexCoord[0].xy;
	ivec3 yPos = ivec3(coord * ySize, 0);
	ivec3 rPos = ivec3(coord * rSize, 0);
	ivec3 bPos = ivec3(coord * bSize, 0);
	
	// Select sample from pixel
	yPos.z = lut[yPos.x & 3];
	rPos.z = lut[rPos.x & 3];
	bPos.z = lut[bPos.x & 3];

	yPos.x >>= 2;
	rPos.x >>= 2;
	bPos.x >>= 2;

	// Lookup & prepare
	vec3 yCrCb = vec3(
		(texelFetch(yPlane, yPos.xy, 0)[yPos.z] - 0.0625) * 1.1643,
		 texelFetch(rPlane, rPos.xy, 0)[rPos.z] - 0.5,
		 texelFetch(bPlane, bPos.xy, 0)[bPos.z] - 0.5
	);

	// YCrCb -> RGB
	gl_FragColor = vec4(
		yCrCb.x + 1.59580 * yCrCb.y,
		yCrCb.x - 0.39173 * yCrCb.z - 0.81290 * yCrCb.y,
		yCrCb.x + 2.01700 * yCrCb.z,
		1.0 // Alpha
	);
}